// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 'git rev-list HEAD --count'.execute([], project.rootDir).text.trim().toInteger()

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion sdk.compileVersion
    buildToolsVersion sdk.buildToolsVersion

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/rxjava.properties'
    }

    defaultConfig {
        applicationId "io.supersimple.duitslandnieuws"
        minSdkVersion sdk.minVersion
        targetSdkVersion sdk.targetVersion
        versionCode versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField 'String', 'GIT_SHA', "\"{$gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
        buildConfigField 'boolean', 'GIT_CLEAN', "${gitIsClean()}"
        buildConfigField 'String', 'GIT_LOCAL_REF', "\"${gitLocalRef()}\""
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime()}\""

        manifestPlaceholders = [apiBaseUrl: "http://www.duitslandnieuws.nl/wp-json/wp/v2/"]

        if (travisBuild) {
            buildConfigField 'String', 'TRAVIS_BUILD_NR', '\"' + System.getenv("TRAVIS_BUILD_NUMBER") + '\"'
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        warning 'MissingTranslation'
        warning 'InvalidPackage'
    }

    signingConfigs {
        release {
            storeFile rootProject.file("duitsland-nieuws-release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "duitsland-nieuws-app"
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        qa {
            applicationIdSuffix '.qa'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }

    configurations.all {
        resolutionStrategy.force libraries.androidSupport
        resolutionStrategy.force libraries.androidDesign
        resolutionStrategy.force libraries.kotlin
        resolutionStrategy.force libraries.kotlinReflect
    }
}

dependencies {
    implementation libraries.androidSupport
    implementation libraries.androidDesign
    //
    // Kotlin
    implementation libraries.kotlin

    // RxJava
    implementation libraries.rxJava
    implementation libraries.rxAndroid

    //Dagger 2
    implementation libraries.dagger
    kapt kaptPlugin.dagger.compiler

    // Requery persistency
    implementation libraries.requery.core
    implementation libraries.requery.kotlin
    implementation libraries.requery.android
    kapt kaptPlugin.requery.processor

    // PaperParcel
    implementation libraries.paperParcel.core
    implementation libraries.paperParcel.kotlin
    kapt kaptPlugin.paperParcel.compiler

    // GSON
    implementation libraries.gson

    // OkHttpClient
    implementation libraries.okHttp.core
    implementation libraries.okHttp.loggingInterceptor

    implementation libraries.retrofit2.core
    implementation libraries.retrofit2.gsonconverter
    implementation libraries.retrofit2.rxjava2

    //
    // SuperSimple
    implementation libraries.superSimple

    // Picasso image loading
    implementation libraries.picasso

    //
    // Test-only dependencies
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestImplementation androidTest.supportAnnotations
    androidTestImplementation androidTest.testRunner
    // Set this dependency to use JUnit 4 rules
    androidTestImplementation androidTest.testRules
    // Espresso
    androidTestImplementation androidTest.espresso.core
    androidTestImplementation(androidTest.espresso.contrib) {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    }
    androidTestImplementation androidTest.espresso.intents

    testImplementation test.junit
    // Mockito
    testImplementation test.mockito.core
    testImplementation (test.mockito.kotlin) {
        exclude group: 'org.mockito', module: 'mockito-core'
    }

    testImplementation test.wiremock
    testImplementation test.slf4j
}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = false
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    teamCitySlug = null
    runOnEachAssemble = true
}
