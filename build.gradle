// Top-level build file where you can add configuration options common to all sub-projects/modules.
//
// Check for outdated dependencies
// https://github.com/ben-manes/gradle-versions-plugin
// ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        jcenter()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath gradlePlugins.android
        classpath gradlePlugins.kotlin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        //
        // Check for outdated dependencies
        // https://github.com/ben-manes/gradle-versions-plugin
        classpath gradlePlugins.versions
        // Keepsafe DexCount
        classpath gradlePlugins.dexcount
        // Detekt
        classpath gradlePlugins.detekt
    }
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply from: rootProject.file('functions.gradle')

    detekt {
        toolVersion = versions.detekt
        input = files("src/main/kotlin")
        config = rootProject.files("default-detekt-config.yml")

        reports {
            xml {
                enabled = true
                destination = file(relativePath("build/reports/detekt/report.xml"))
            }
            html {
                enabled = true
                destination = file(relativePath("build/reports/detekt/report.html"))
            }
        }
    }

    repositories {
        jcenter()
        google()
        maven { url "https://jitpack.io" }
    }
    
    plugins.apply('checkstyle')
    
    checkstyle {
        toolVersion = '8.7'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
    
    task('checkstyle', type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        
        classpath = files()
    }
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

