// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: rootProject.file('dependencies.gradle')
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath gradlePlugins.android
        classpath gradlePlugins.kotlin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        //
        // Check for outdated dependencies
        // https://github.com/ben-manes/gradle-versions-plugin
        classpath gradlePlugins.versions
        // Keepsafe DexCount
        classpath gradlePlugins.dexcount
        // Koin
        classpath gradlePlugins.koin
    }
}

allprojects {
    apply from: rootProject.file('functions.gradle')

    repositories {
        jcenter()
        google()
    }

    //
    // Check for outdated dependencies
    // https://github.com/ben-manes/gradle-versions-plugin
    // ./gradlew dependencyUpdates -Drevision=release
    plugins.apply('com.github.ben-manes.versions')
    plugins.apply('checkstyle')
    plugins.apply('koin')

    checkstyle {
        toolVersion = '8.7'
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }
    
    task('checkstyle', type: Checkstyle) {
        configFile rootProject.file('checkstyle.xml')
        source 'src/main/java'
        ignoreFailures false
        showViolations true
        include '**/*.java'
        
        classpath = files()
    }
}

ext {
    ciBuild = System.getenv("TRAVIS") == "true"
    ciBuildNr = System.getenv("TRAVIS_BUILD_NUMBER")
    // allows for -Dpre-dex=false to be set
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
